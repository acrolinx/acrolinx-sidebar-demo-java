plugins {
    id 'acrolinx-sidebar-demo.java-application-conventions'
}

final String SWT_VERSION = '3.123.0'
final String OSGI_PLATFORM = getOsgiPlatformValue()

configurations.configureEach {
    resolutionStrategy.dependencySubstitution {
        substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') using module("org.eclipse.platform:org.eclipse.swt.$OSGI_PLATFORM:$SWT_VERSION")
    }
}

dependencies {
    implementation libs.sidebar.sdk.java
    implementation "org.eclipse.platform:org.eclipse.swt.$OSGI_PLATFORM:$SWT_VERSION"

    runtimeOnly libs.slf4j.simple
}

application {
    mainClass = 'com.acrolinx.client.sidebar.demo.swt.SwtAcrolinxApplication'
}

private static String getOsgiPlatformValue() {
    String osName = System.getProperty('os.name').toLowerCase()
    String osArch = System.getProperty('os.arch')

    if (osName.contains('windows')) {
        return 'win32.win32.x86_64'
    } else if (osName.contains('linux')) {
        return 'gtk.linux.x86_64'
    } else if (osName.contains('mac')) {
        return 'cocoa.macosx.' + osArch
    }

    throw new IllegalStateException('unknown OS name: ' + osName)
}
