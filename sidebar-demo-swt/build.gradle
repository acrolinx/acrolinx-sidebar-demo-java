plugins {
    id 'acrolinx-sidebar-demo.java-application-conventions'
}

final String SWT_VERSION = '3.123.0'
final String OSGI_PLATFORM = getOsgiPlatformValue()

configurations.configureEach {
    resolutionStrategy.dependencySubstitution {
        substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') using module("org.eclipse.platform:org.eclipse.swt.$OSGI_PLATFORM:$SWT_VERSION")
    }
}

dependencies {
    implementation libs.sidebar.sdk
    implementation "org.eclipse.platform:org.eclipse.swt.$OSGI_PLATFORM:$SWT_VERSION"
}

application {
    mainClass = 'com.acrolinx.client.sidebar.demo.swt.AcrolinxDemoClientSwt'
}

private static String getOsgiPlatformValue() {
    String osName = System.getProperty('os.name').toLowerCase()

    if (osName.contains('windows')) {
        return 'win32.win32.x86_64'
    } else if (osName.contains('linux')) {
        return 'gtk.linux.x86_64'
    } else if (osName.contains('mac')) {
        return 'cocoa.macosx.x86_64'
    }

    throw new GradleException('unknown OS name: ' + osName)
}
