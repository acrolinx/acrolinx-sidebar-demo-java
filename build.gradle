/*
 * Copyright (c) 2016-2017 Acrolinx GmbH
 */

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.diffplug.gradle:goomph:+"
        classpath "net.researchgate:gradle-release:2.4.0"
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

subprojects {

    group 'com.acrolinx'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "http://maven-eclipse.github.io/maven"
        }
    }

    dependencies {
        compile 'com.google.guava:guava:19.+'
        compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.21'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
}

project(':sidebar_demo_swt') {

    apply plugin: 'application'
    apply plugin: 'com.diffplug.gradle.swt.nativedeps'

    mainClassName = "com.acrolinx.client.sidebar.demo.swt.AcrolinxDemoClientSWT"

    applicationDefaultJvmArgs = ["-Dsun.net.http.allowRestrictedHeaders=true", "-Djava.util.logging.config.file=logging.properties"]

    sourceSets.main.resources.srcDir "../../sidebar/swt/src/main/resources";

    dependencies {
        compile project(':sidebar_swt')
        testCompile project(':sidebar_swt')
    }
}

project(':sidebar_demo_swing') {
    apply plugin: 'application'

    mainClassName = "com.acrolinx.client.sidebar.demo.swing.AcrolinxDemoClientSwing"

    applicationDefaultJvmArgs = ["-Dsun.net.http.allowRestrictedHeaders=true", "-Djava.util.logging.config.file=logging.properties"]

    dependencies {
        compile project(':sidebar_swing')
    }
}

project(':sidebar_demo_jfx') {
    apply plugin: 'application'

    mainClassName = "com.acrolinx.client.sidebar.demo.jfx.AcrolinxDemoClientJFX"

    applicationDefaultJvmArgs = ["-Dsun.net.http.allowRestrictedHeaders=true", "-Djava.util.logging.config.file=logging.properties"]

    dependencies {
        compile project(':sidebar_jfx')
    }
}

project(':sidebar_jfx') {

    dependencies {
        compile project(':sidebar_common')
    }
}

project(':sidebar_swing') {

    dependencies {
        compile project(':sidebar_jfx')
    }
}

project(':sidebar_swt') {

    apply plugin: 'com.diffplug.gradle.swt.nativedeps'

    dependencies {
        compile project(':sidebar_common')
    }
}

project(':sidebar_common') {

    dependencies {
        compile 'com.google.code.gson:gson:2.2.4'
        compile 'commons-lang:commons-lang:2.6'
        compile 'org.bitbucket.cowwoc:diff-match-patch:1.1'
    }
}

project(':java-sdk-demo-build'){
    afterEvaluate {

        rootProject.apply plugin: 'net.researchgate.release'

        rootProject.release {
            preTagCommitMessage = '[Gradle Release Plugin] - Release tag: '
            newVersionCommitMessage = '[Gradle Release Plugin] - New SNAPSHOT-version: '
            scmAdapters = [net.researchgate.release.GitAdapter]
            git {
                requireBranch = "release|master"
            }
        }

        rootProject.afterReleaseBuild.dependsOn uploadArchives
    }
}

