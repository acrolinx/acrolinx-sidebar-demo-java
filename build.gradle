import org.gradle.internal.jvm.Jvm
import org.gradle.internal.os.OperatingSystem

/*
* Copyright (c) 2016-present Acrolinx GmbH
*/

plugins {
	id "com.github.spotbugs" version "4.5.1"
	id "org.openjfx.javafxplugin" version "0.0.9" apply false
	id "com.github.jk1.dependency-license-report" version "1.16"
	id "com.github.johnrengelman.shadow" version "5.2.0"
	id "org.ajoberstar.grgit" version "4.1.0"

	id 'se.patrikerdes.use-latest-versions' version '0.2.15'
	id 'com.github.ben-manes.versions' version '0.33.0'
	id "org.sonarqube" version "4.0.0.2929"
}

apply plugin: 'org.ajoberstar.grgit'
ext.grgit = grgit.open(currentDir: rootDir)

println("Current JVM Version " + Jvm.current().getJavaVersion());

switch ( OperatingSystem.current() ) {
	case OperatingSystem.WINDOWS:
		project.ext.swtNatives = "org.eclipse.swt.win32.win32.x86_64"
		break
	case OperatingSystem.LINUX:
		project.ext.swtNatives = "org.eclipse.swt.gtk.linux.x86_64"
		break
	case OperatingSystem.MAC_OS:
		project.ext.swtNatives = "org.eclipse.swt.cocoa.macosx.x86_64"
		break
}

allprojects {

	/*tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
		options.memberLevel = JavadocMemberLevel.PUBLIC
	}*/

	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
		}
	}


	project.version = currentVersion
}

subprojects {

	group 'com.acrolinx'

	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'

	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

	compileJava.options.encoding = 'UTF-8'
	compileTestJava.options.encoding = 'UTF-8'

	configurations.all {
		// Check for updates every build
		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
	}

	configurations {
		compileSuperFatJar

		// Disable this setting if you don't want to have the JFX dependencies for ALL platforms in a fat jar.
		// By disabling only the dependencies for your current (build hosting) platform will be included (in JRE 11).
		implementation.extendsFrom(compileSuperFatJar)
	}

	repositories {
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
	}

	configurations.all {
		resolutionStrategy {
			dependencySubstitution {
				// The maven property ${osgi.platform} is not handled by Gradle
				// so we replace the dependency, using the osgi platform from the project settings
				def os = System.getProperty("os.name").toLowerCase()
				if (os.contains("windows")) {
					substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.win32.win32.x86_64:$SWT_VERSION")
				}
				else if (os.contains("linux")) {
					substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.gtk.linux.x86_64:$SWT_VERSION")
				}
				else if (os.contains("mac")) {
					substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.cocoa.macosx.x86_64:$SWT_VERSION")
				}
			}
		}
	}

	dependencies {
		implementation("com.acrolinx.client:sidebar-sdk:${JAVA_SDK_VERSION}") { changing = true }
		testImplementation group: 'junit', name: 'junit', version: '4.+'
	}

	apply plugin: 'com.github.spotbugs'
	spotbugs {
		toolVersion = '3.1.12'
		ignoreFailures = false    // bug free or it doesn't ship!
		reportsDir = file('build/findbugs')
		effort = 'max'            // min|default|max
		reportLevel = 'medium'        // low|medium|high (low = sensitive to even minor mistakes)
		excludeFilter = file("$rootProject.projectDir/findbugs/excludeFilter.xml")
		omitVisitors = []        // bugs that we want to ignore
	}
}

project(':sidebar_demo_swt') {

	apply plugin: 'application'

	mainClassName = "com.acrolinx.client.sidebar.demo.swt.AcrolinxDemoClientSWT"

	sourceSets.main.resources.srcDir "../../sidebar/swt/src/main/resources";
	jar {
		duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
		manifest {
			attributes 'Main-Class': "com.acrolinx.client.sidebar.demo.swt.AcrolinxDemoClientSWT"
		}
		from {
			configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
		}
	}

	javadoc.enabled = false

	dependencies {
		compileSuperFatJar "org.eclipse.platform:$swtNatives:$SWT_VERSION"
	}


}

project(':sidebar_demo_swing') {
	apply plugin: 'application'

	if (Integer.parseInt(Jvm.current().getJavaVersion().getMajorVersion()) >= 11) {
		apply plugin: 'org.openjfx.javafxplugin'

		javafx {
			version = "${JFX_VERSION}"
			modules = ["javafx.web", "javafx.swing"]
		}
	}

	mainClassName = "com.acrolinx.client.sidebar.demo.swing.AcrolinxDemoClientSwing"
	jar {
		duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
		manifest {
			attributes 'Main-Class': "com.acrolinx.client.sidebar.demo.swing.AcrolinxDemoClientSwing"
		}
		from {
			configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
		}
	}

	dependencies {
		compileSuperFatJar "org.openjfx:javafx-base:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-base:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-base:${JFX_VERSION}:mac"
		compileSuperFatJar "org.openjfx:javafx-web:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-web:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-web:${JFX_VERSION}:mac"
		compileSuperFatJar "org.openjfx:javafx-swing:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-swing:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-swing:${JFX_VERSION}:mac"
		compileSuperFatJar "org.openjfx:javafx-graphics:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-graphics:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-graphics:${JFX_VERSION}:mac"
		compileSuperFatJar "org.openjfx:javafx-controls:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-controls:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-controls:${JFX_VERSION}:mac"
		compileSuperFatJar "org.openjfx:javafx-media:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-media:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-media:${JFX_VERSION}:mac"
	}

	javadoc.enabled = false
}

project(':sidebar_demo_jfx') {
	apply plugin: 'application'

	if (Integer.parseInt(Jvm.current().getJavaVersion().getMajorVersion()) >= 11) {
		apply plugin: 'org.openjfx.javafxplugin'
		javafx {
			version = "${JFX_VERSION}"
			modules = ["javafx.web"]
		}
	}
	mainClassName = "com.acrolinx.client.sidebar.demo.jfx.AcrolinxDemoClientJFX"

	javadoc.enabled = false

	jar {
		duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
		manifest {
			attributes 'Main-Class': "com.acrolinx.client.sidebar.demo.jfx.Launcher"
		}
		from {
			configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
		}
	}

	dependencies {
		compileSuperFatJar "org.openjfx:javafx-base:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-base:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-base:${JFX_VERSION}:mac"
		compileSuperFatJar "org.openjfx:javafx-web:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-web:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-web:${JFX_VERSION}:mac"
		compileSuperFatJar "org.openjfx:javafx-graphics:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-graphics:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-graphics:${JFX_VERSION}:mac"
		compileSuperFatJar "org.openjfx:javafx-controls:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-controls:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-controls:${JFX_VERSION}:mac"
		compileSuperFatJar "org.openjfx:javafx-media:${JFX_VERSION}:win"
		compileSuperFatJar "org.openjfx:javafx-media:${JFX_VERSION}:linux"
		compileSuperFatJar "org.openjfx:javafx-media:${JFX_VERSION}:mac"
	}
}

project(':java-sdk-demo-build') {

	apply plugin: 'java-library-distribution'
	apply plugin: 'com.github.johnrengelman.shadow'

	def buildNumber = System.getenv('BUILD_NUMBER') || System.getenv('TRAVIS_BUILD_NUMBER')

	def buildVersion = ((buildNumber != null) ? buildNumber : 123)

	def artifactName = "acrolinx-sidebar-java-demo"
	def fullVersion = "${project.version}-${buildVersion}"

	distTar.enabled = false
	distZip.enabled = false

	dependencies {
		implementation project(':sidebar_demo_jfx')
		implementation project(':sidebar_demo_swing')
		implementation project(':sidebar_demo_swt')
	}

	task createJar(type: Jar) {
		manifest {
			attributes(
					"Implementation-Title": "${artifactName}",
					"Implementation-Version": "${fullVersion}",
					"Specification-Title": "${artifactName}",
					"Specification-Version": project.version
			)
		}
	}

	shadowJar {
		archiveFileName = "${artifactName}-${fullVersion}.jar"
		exclude 'META-INF/*.DSA'
		exclude 'META-INF/*.RSA'
		destinationDirectory = file("${buildDir}/lib")
		manifest {
			inheritFrom project.tasks.createJar.manifest
		}
		configurations = [project.configurations.compileClasspath]
	}

	task createStartScriptsJFX(type: CreateStartScripts) {
		outputDir = file("${buildDir}/bin/jfx")
		mainClass = 'com.acrolinx.client.sidebar.demo.jfx.Launcher'
		applicationName = 'acrolinxDemoClientJFX'
		classpath = files("${artifactName}-${fullVersion}.jar")
	}

	task createStartScriptsSwing(type: CreateStartScripts) {
		outputDir = file("${buildDir}/bin/swing")
		mainClass = 'com.acrolinx.client.sidebar.demo.swing.AcrolinxDemoClientSwing'
		applicationName = 'acrolinxDemoClientSwing'
		classpath = files("${artifactName}-${fullVersion}.jar")
	}

	task createStartScriptsSWT(type: CreateStartScripts) {
		outputDir = file("${buildDir}/bin/swt")
		mainClass = 'com.acrolinx.client.sidebar.demo.swt.AcrolinxDemoClientSWT'
		applicationName = 'acrolinxDemoClientSWT'
		classpath = files("*")
	}

	task zipDist(type: Zip) {

		delete fileTree('../dist') {
			include '*.zip'
		}

		try {
			grgit.remove(patterns: ['dist'])
		} catch (Exception e) {
			// DO nothing
		}


		def baseDir = "${artifactName}-${fullVersion}.zip"
		archiveFileName = baseDir

		destinationDirectory = file('../dist')
		into(baseDir) {
			into("lib") {
				from(shadowJar)
			}
		}
		into(baseDir) {
			into("bin") {
				from(createStartScriptsJFX)
			}
		}
		into(baseDir) {
			into("bin") {
				from(createStartScriptsSwing)
			}
		}
		into(baseDir) {
			into("bin") {
				from(createStartScriptsSWT)
			}
		}
		into(baseDir) {
			from("../LICENSE")
		}
	}

	artifacts {
		archives zipDist
	}

	task tagAndReleaseIfNewVersion(dependsOn: zipDist) {
		doLast {
			def hasOldReleaseVersion = grgit.tag.list().find { it.getName() == "release-${currentVersion}" }
			if (!fullVersion.contains('SNAPSHOT') && !hasOldReleaseVersion) {
				grgit.commit(message: "Release version ${currentVersion}")
				grgit.push(force: true)
				grgit.tag.add(name: "release-${currentVersion}", message: "Releasing ${currentVersion}")
				grgit.push(tags: true)
			}

		}
	}
}

sonar {
	properties {
		property "sonar.projectKey", "acrolinx_acrolinx-sidebar-demo-java"
		property "sonar.organization", "acrolinx"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}
